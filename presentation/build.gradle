plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
}

android {
    compileSdkVersion versions.application.targetSdk
    buildToolsVersion versions.application.buildTools

    defaultConfig {
        applicationId "com.senyk.volodymyr.schedulesapp"
        minSdkVersion versions.application.minSdk
        targetSdkVersion versions.application.targetSdk
        versionName "$versions.application.versionName (${getVersion()})"
        versionCode versions.application.versionCode
    }

    signingConfigs {
        debug {
            storeFile file('../keystore/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile file(getKeystoreProperty('releaseStoreFile'))
            storePassword getKeystoreProperty('releaseStorePassword')
            keyAlias getKeystoreProperty('releaseKeyAlias')
            keyPassword getKeystoreProperty('releaseKeyPassword')
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
            debuggable true
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        dataBinding true
    }
}

String getKeystoreProperty(String propertyName) {
    def keystorePropertiesFile = rootProject.file("keystore/keystore.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }
    if (keystoreProperties.containsKey(propertyName)) {
        return keystoreProperties[propertyName]
    } else if (System.getenv(propertyName) != null) {
        return System.getenv(propertyName)
    } else {
        throw new IllegalStateException("Keystore property not found: $propertyName")
    }
}

dependencies {

    // modules
    implementation project(":data")
    implementation project(":domain")
    implementation project(":core")

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin.language"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin.language"
    implementation "androidx.core:core-ktx:$versions.kotlin.ktx"

    // view
    implementation "androidx.fragment:fragment-ktx:$versions.view.ktx_fragment"
    implementation "androidx.appcompat:appcompat:$versions.view.appcompat"
    implementation "com.google.android.material:material:$versions.view.material"
    implementation "androidx.constraintlayout:constraintlayout:$versions.view.constraint"
    implementation "androidx.viewpager2:viewpager2:$versions.view.viewpager"

    // di
    implementation "com.google.dagger:dagger:$versions.di.dagger"
    implementation "com.google.dagger:dagger-android-support:$versions.di.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.di.dagger"
    kapt "com.google.dagger:dagger-android-processor:$versions.di.dagger"

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation.arch_component"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation.arch_component"

    // lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
    kapt "androidx.lifecycle:lifecycle-common-java8:$versions.lifecycle"

    // persistence
    implementation "androidx.room:room-runtime:$versions.persistence.room"
    implementation "androidx.room:room-ktx:$versions.persistence.room"
    implementation "androidx.room:room-rxjava2:$versions.persistence.room"
    kapt "androidx.room:room-compiler:$versions.persistence.room"

    // rx
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rx.android"
    implementation "io.reactivex.rxjava2:rxjava:$versions.rx.java"
    implementation "io.reactivex.rxjava2:rxkotlin:$versions.rx.kotlin"

    // frameworks
    implementation "com.hannesdorfmann:adapterdelegates4:$versions.frameworks.adapterdelegates"
}
